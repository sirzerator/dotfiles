set nocompatible
filetype plugin indent on

" Utility methods
function! g:IsOnBattery()
	return readfile('/sys/class/power_supply/AC/online') == ['0']
endfunction

function! BuildYCM(info)
	if a:info.status == 'installed' || a:info.force
		!./install.py
	endif
endfunction

" Plugins
call plug#begin()
	Plug 'elixir-lang/vim-elixir'
	Plug 'evanleck/vim-svelte', { 'branch': 'main' }
	Plug 'fatih/vim-go'
	Plug 'itchyny/lightline.vim'
	Plug 'jceb/vim-orgmode'
	Plug 'junegunn/fzf'
	Plug 'junegunn/fzf.vim'
	Plug 'jwalton512/vim-blade'
	Plug 'kchmck/vim-coffee-script'
	Plug 'leafgarland/typescript-vim'
	Plug 'mbbill/undotree'
	Plug 'mhinz/vim-signify'
	Plug 'mxw/vim-jsx'
	Plug 'nanotech/jellybeans.vim', { 'tag': 'v1.7' }
	Plug 'noahfrederick/vim-skeleton'
	Plug 'pangloss/vim-javascript'
	Plug 'phpactor/phpactor', {'for': 'php', 'tag': '*', 'do': 'composer install --no-dev -o'}
	Plug 'posva/vim-vue'
	Plug 'raimondi/delimitmate'
	Plug 'rust-lang/rust.vim'
	Plug 'slashmili/alchemist.vim'
	Plug 'tpope/vim-fugitive'
	Plug 'tpope/vim-rails', { 'for': ['ruby', 'eruby'] }
	Plug 'tpope/vim-repeat'
	Plug 'tpope/vim-speeddating'
	Plug 'tpope/vim-surround'
	Plug 'valloric/youcompleteme', { 'do': function('BuildYCM') }
	Plug 'vim-ruby/vim-ruby', { 'for': ['ruby', 'eruby'] }
	Plug 'vim-scripts/matchit.zip'
	Plug 'w0rp/ale'
	Plug 'wincent/terminus'
call plug#end()

runtime! config/**/*.vim

" Configuration
set number               " show line numbers
set ruler                " show the cursor position all the time
set laststatus=2         " always display status bar
set wildmenu             " 
set cursorline           " highlight the current line
set shortmess=xtlI       " shorten some status messages
set mouse=               " disable mouse
set showcmd              " display incomplete commands
if &history < 10000
	set history=10000    " command history
endif

set autoindent
set smarttab
" make backspace behave like most other apps
set backspace=indent,eol,start

set complete-=i          " don't autocomplete in insert mode
set display+=lastline    " display as much as possible of a long line
set scrolloff=2          " display x lines above or below the cursor
set sidescrolloff=5      " display y columns before or after the cursor
set colorcolumn=120      " color background at this column

set wrap linebreak showbreak=...
set list listchars=eol:$,tab:▸\ ,nbsp:¬,trail:·
set nolist
nnoremap <F2> :Git<CR>
nnoremap <F8> :set list!<CR>
nnoremap <F10> :redraw!<CR>

let g:netrw_banner=0
" Open netrw at current buffer path
command! E :e %:p:h
autocmd filetype netrw nmap <buffer> o <cr>

" nnoremap <F3> :set paste!<CR> " Handled by terminus
nnoremap <F5> :make<CR>

" Wrapped lines navigation that doesn't break counts
noremap <silent> <expr> j (v:count == 0 ? 'gj' : 'j')
noremap <silent> <expr> k (v:count == 0 ? 'gk' : 'k')
noremap <silent> <expr> <Down> (v:count == 0 ? 'gj' : 'j')
noremap <silent> <expr> <Up> (v:count == 0 ? 'gk' : 'k')

set incsearch            " do incremental search
set hlsearch             " do incremental search
set ignorecase smartcase " ignore case except when mixed case
" Use <F4> to clear the highlighting of :set hlsearch.
nnoremap <silent> <F4> :nohlsearch<C-R>=has('diff')?'<Bar>diffupdate':''<CR><CR><C-L>

" Search for the current selection (instead of word)
xnoremap * :<C-u>call <SID>VSetSearch('/')<CR>/<C-R>=@/<CR><CR>
xnoremap # :<C-u>call <SID>VSetSearch('?')<CR>?<C-R>=@/<CR><CR>

function! s:VSetSearch(cmdtype)
	let temp = @
	norm! gv"sy
	let @/ = '\V' . substitute(escape(@s, a:cmdtype.'\'), '\n', '\\n', 'g')
	let @s = temp
endfunction

syntax on
set background=dark
colorscheme jellybeans
" " Allow color schemes to do bright colors without forcing bold.
" if &t_Co == 8 && $TERM !~# '^linux\|^Eterm'
" 	set t_Co=16
" endif

" Default indentation scheme
set noexpandtab
"set expandtab
set shiftwidth=4
set tabstop=4
set softtabstop=4

" Rotate tab sizes
nnoremap <silent> <F6> :let &ts=(&ts*2 > 16 ? 2 : &ts*2)<CR>:let &sw=&ts<CR>:let &sts=&ts<CR>:echo "Tab size: " . &ts<CR>
nnoremap <silent> <F7> :set expandtab!<CR>:set expandtab?<CR>

" Set backup directories to sensible values
set backupdir=~/.vim/backup
set directory=~/.vim/swap
set undofile
set undodir=~/.vim/undo

" Easier split navigation
nnoremap <C-J> <C-W><C-J>
nnoremap <C-K> <C-W><C-K>
nnoremap <C-L> <C-W><C-L>
" Remap NetRwRefresh to avoid conflict
nmap <unique> <C-.> <Plug>NetrwRefresh
nnoremap <C-H> <C-W><C-H>

" Avoid losing undo history on ctrl-w and ctrl-u
inoremap <C-U> <C-G>u<C-U>
inoremap <C-W> <C-G>u<C-W>

" Invert split opening
set splitbelow
set splitright

" Easier tab navigation
map T^ :tabfirst<CR>
map T$ :tablast<CR>
map TH :tabprev<CR>
map Th :tabprev<CR>
map TL :tabnext<CR>
map Tl :tabnext<CR>
map TT :tabnew<CR>
map Tt :tabnew<CR>
map TC :tabclose<CR>
map Tc :tabclose<CR>

" Smart linejoins
if v:version > 703 || v:version == 703 && has('patch541')
	set formatoptions+=j
endif

" Enter and Backspace operate on paragraphs
nnoremap <BS> {
onoremap <BS> {
vnoremap <BS> {
nnoremap <expr> <CR> empty(&buftype) ? '}' : '<CR>'
onoremap <expr> <CR> empty(&buftype) ? '}' : '<CR>'
vnoremap <CR> }

" Keep selections when indenting
vnoremap < <gv
vnoremap > >gv

" Remove whitespace in selected file types
autocmd FileType coffee,ruby,eruby,html,scss,erlang,javascript,php,sh,python,markdown,rust,vue,typescript autocmd BufWritePre <buffer> :%s/\s\+$//e

" Convenient command to see the difference between the current buffer and the
" file it was loaded from, thus the changes you made.
" Only define it when not defined already.
if !exists(":DiffOrig")
	command DiffOrig vert new | set bt=nofile | r # | 0d_ | diffthis
	\ | wincmd p | diffthis
endif

" Hide banner in netrw ssh navigation
let g:netrw_sftp_cmd="sftp -q"

" Terminal
map <Leader>tv :vert term ++close<cr>
tmap <Leader>tv <c-w>vert :term ++close<cr>
map <Leader>th :term ++close<cr>
tmap <Leader>th <c-w>:term ++close<cr>
map <Leader>T :tab term ++close<cr>
tmap <Leader>T <c-w>:tab term ++close<cr>
set termwinkey=<C-[>

"Use 24-bit (true-color) mode in Vim/Neovim when outside tmux.
"If you're using tmux version 2.2 or later, you can remove the outermost $TMUX check and use tmux's 24-bit color support
"(see < http://sunaku.github.io/tmux-24bit-color.html#usage > for more information.)
if (empty($TMUX))
  if (has("nvim"))
    "For Neovim 0.1.3 and 0.1.4 < https://github.com/neovim/neovim/pull/2198 >
    let $NVIM_TUI_ENABLE_TRUE_COLOR=1
  endif
  "For Neovim > 0.1.5 and Vim > patch 7.4.1799 < https://github.com/vim/vim/commit/61be73bb0f965a895bfb064ea3e55476ac175162 >
  "Based on Vim patch 7.4.1770 (`guicolors` option) < https://github.com/vim/vim/commit/8a633e3427b47286869aa4b96f2bfc1fe65b25cd >
  " < https://github.com/neovim/neovim/wiki/Following-HEAD#20160511 >
  if (has("termguicolors"))
    set termguicolors
  endif
endif
